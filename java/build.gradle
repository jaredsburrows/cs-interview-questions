import static org.gradle.api.JavaVersion.VERSION_11
import static org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
import static org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED
import static org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED

plugins {
    id 'java-library'
    id 'groovy'
    alias(libs.plugins.license)
}

java {
    sourceCompatibility = VERSION_11.toString()
    targetCompatibility = VERSION_11.toString()

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(VERSION_11.toString()))
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = VERSION_11
    targetCompatibility = VERSION_11

    configure(options) {
        compilerArgs << '-Xlint:all'
        compilerArgs << '-Xlint:-options'
        encoding = 'utf-8'
        fork = true
    }
}

tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = VERSION_11.toString()
    targetCompatibility = VERSION_11.toString()

    configure(options) {
        compilerArgs << '-Xlint:all'
        compilerArgs << '-Xlint:-options'
        encoding = 'utf-8'
        fork = true
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    testLogging {
        exceptionFormat = FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        events = [FAILED, SKIPPED]
    }

    def maxWorkerCount = gradle.startParameter.maxWorkerCount
    maxParallelForks = (maxWorkerCount < 2) ? 1 : maxWorkerCount / 2
}

dependencies {
    compileOnly libs.findbugs

    testRuntimeOnly libs.junit.engine

    testImplementation localGroovy()
    testImplementation libs.spockcore, { exclude module: 'groovy-all' } // Use localGroovy()
    testImplementation libs.equalsverifier
}
