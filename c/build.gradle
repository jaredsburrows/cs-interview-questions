plugins {
    id 'c'
    id 'cunit-test-suite'
}

def hasCUnit = file('/usr/local/Cellar/cunit/2.1-3/include').directory

model {
    repositories {
        libs(PrebuiltLibraries) {
            // TODO dynamically download
            if (hasCUnit) {
                cunit {
                    headers.srcDir '/usr/local/Cellar/cunit/2.1-3/include'

                    binaries.withType(StaticLibraryBinary) {
                        staticLibraryFile = file('/usr/local/Cellar/cunit/2.1-3/lib/libcunit.a')
                    }
                }
            }
        }
    }

    components {
        main(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c'
                        include '**/*.c'
                    }

                    exportedHeaders {
                        srcDir 'src/main/include'
                        include '**/*.h'
                    }
                }
            }
        }
    }

    testSuites {
        if (hasCUnit) {
            mainTest(CUnitTestSuiteSpec) {
                sources {
                    c {
                        source {
                            srcDir 'src/test/c'
                            include '**/*.c'
                        }

                        exportedHeaders {
                            srcDir 'src/test/include'
                            include '**/*.h'
                        }
                    }
                }

                testing $.components.main
            }
        }
    }

    binaries {
        all {
            if (toolChain in Gcc || toolChain in Clang) {
//                cCompiler.args '-std=c11', '-Wall', '-Wextra', '-Werror', '-O3', '-pedantic'
                cCompiler.args '-std=c11', '-Wall', '-Wextra', '-O3', '-pedantic'
            }

            if (toolChain in VisualCpp) {
                cCompiler.args '/Wall', '/Wx', '/O1', '/O2', '/Ox'
            }
        }

        if (hasCUnit) {
            withType(CUnitTestSuiteBinarySpec) {
                lib library: 'cunit', linkage: 'static'
            }
        }
    }
}
