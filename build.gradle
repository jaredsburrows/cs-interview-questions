plugins {
    alias(libs.plugins.versions)
    alias(libs.plugins.license) apply false
    id 'test-report-aggregation'
    id 'idea'
}

allprojects {
    configurations.configureEach {
        resolutionStrategy {
            preferProjectModules()

            enableDependencyVerification()

            eachDependency { details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion libs.versions.kotlin.get()
                }
            }
        }
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }
}

tasks.withType(Wrapper).configureEach {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.register('testReport', TestReport) {
    dependsOn project(':docs').tasks.named('asciidoctor')
    destinationDirectory = project(':docs').file('build/docs/asciidoc/tests')
    subprojects.each {
        it.tasks.withType(Test).each { testTask ->
            testResults.from(testTask)
        }
    }
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory
}
